{% raw %}
set dotenv-load

ARGS_TEST := env("_UV_RUN_ARGS_TEST", "")

@_:
    just --list

# run tests
[group('qa')]
test *args:
    uv run {{ ARGS_TEST }} -m pytest {{ args }}

_cov *args:
    uv run -m coverage {{ args }}
{% endraw %}
# run tests and measure coverage
[group('qa')]
@cov:
    just _cov erase
    just _cov run -m pytest tests
    # Ensure entrypoint is importable.
    # You can also use coverage to run your CLI entrypoints.
    just _cov run -m {{ package_name }} hello -n Everyone
    just _cov combine
    just _cov report
    just _cov html

# run linters
[group('qa')]
lint:
    uvx ruff check
    uvx ruff format

# perform all checks
[group('qa')]
check-all: lint cov

# Remove temporary files
[group('lifecycle')]
clean:
    rm -rf .venv .pytest_cache .ruff_cache .coverage htmlcov
    rm -rf src/*.egg-info
    find . -type d -name "__pycache__" -exec rm -r {} +

# Recreate project virtualenv from nothing
[group('lifecycle')]
fresh: clean install
